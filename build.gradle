plugins {
    id 'java-gradle-plugin'
    id 'java-test-fixtures'
    id 'jacoco'
    id 'maven-publish'
    id 'org.ajoberstar.reckon' version '0.13.0'
    id 'org.sonarqube' version '3.3'
    id "com.gradle.plugin-publish" version "0.18.0"
    id 'be.vbgn.ci-detect' version '0.5.0'
    id 'be.vbgn.dev-conventions.opinion' version '0.5.0'
    id 'nebula.integtest' version '9.1.6'
}

group 'be.vbgn.gradle'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:4.0.0'
    testFixturesImplementation 'org.mockito:mockito-core:3.12.4'
    integTestImplementation 'commons-io:commons-io:2.11.0'
}

gradlePlugin {
    plugins {
        buildAspects {
            id = 'be.vbgn.build-aspects'
            implementationClass = 'be.vbgn.gradle.buildaspects.BuildAspectsPlugin'
        }
    }
    testSourceSets(sourceSets.integTest)
}

pluginBundle {
    vcsUrl = "https://github.com/vierbergenlars/build-aspects-gradle-plugin"
    website = vcsUrl
    description = "A gradle plugin that manages different variants for your project."
    tags = ["aspects", "variant", "build-variant"]
    plugins {
        buildAspects {
            displayName = "Build Aspects Plugin"
        }
    }
}


import org.gradle.util.GradleVersion

integrationTest {
    dependsOn(publishToMavenLocal)
    doFirst {
        systemProperty "pluginVersion", project.version
        if (gradle.startParameter.offline) {
            systemProperty "be.vbgn.gradle.buildaspects.integration.useGradleVersion", GradleVersion.current().version
            systemProperty "be.vbgn.gradle.buildaspects.integration.forceCurrentGradleVersion", "true"
        }
    }
}

integrationTest.shouldRunAfter(test)

sonarqube {
    properties {
        properties["sonar.tests"] += sourceSets.integTest.allSource.srcDirs
    }
}
