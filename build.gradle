plugins {
    id 'java-gradle-plugin'
    id 'jacoco'
    id 'maven-publish'
    id 'org.ajoberstar.reckon' version '0.12.0'
    id 'org.sonarqube' version '3.0'
    id "com.gradle.plugin-publish" version "0.12.0"
}

group 'be.vbgn.gradle'

sourceCompatibility = 1.8

sourceSets {
    integrationTest {

    }
}

repositories {
    jcenter()
}

configurations {
    integrationTestImplementation.extendsFrom(testImplementation)
}

dependencies {
    testImplementation 'junit:junit:4.13'
    testImplementation 'org.mockito:mockito-core:3.4.4'
    integrationTestImplementation 'commons-io:commons-io:2.7'
}

gradlePlugin {
    plugins {
        buildAspects {
            id = 'be.vbgn.build-aspects'
            implementationClass = 'be.vbgn.gradle.buildaspects.BuildAspectsPlugin'
        }
    }
    testSourceSets(sourceSets.integrationTest)
}

pluginBundle {
    vcsUrl = "https://github.com/vierbergenlars/build-aspects-gradle-plugin"
    website = vcsUrl
    description = "A gradle plugin that manages different variants for your project."
    tags = ["aspects", "variant", "build-variant"]
    plugins {
        buildAspects {
            displayName = "Build Aspects Plugin"
        }
    }
}


import org.gradle.util.GradleVersion

task integrationTest(type: Test, group: 'verification') {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    dependsOn(publishToMavenLocal)
    doFirst {
        systemProperty "pluginVersion", project.version
        if (gradle.startParameter.offline) {
            systemProperty "be.vbgn.gradle.buildaspects.integration.useGradleVersion", GradleVersion.current().version
            systemProperty "be.vbgn.gradle.buildaspects.integration.forceCurrentGradleVersion", "true"
        }
    }
}

integrationTest.shouldRunAfter(test)
check.dependsOn(integrationTest)
tasks.sonarqube.dependsOn(check)

sonarqube {
    properties {
        properties["sonar.tests"] += sourceSets.integrationTest.allSource.srcDirs
    }
}


reckon {
    scopeFromProp()
    snapshotFromProp()
}

reckonTagCreate.dependsOn(check)
